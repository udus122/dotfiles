# Function to perform simple arithmetic operations using awk
function calc() {
  awk "BEGIN {print $*}"
}

function find-ghq () {
  # select local repository in ghq
  declare -r REPO_NAME="$(ghq list | fzf )";
  [[ -n "${REPO_NAME}" ]] && cd "$(ghq root)/${REPO_NAME}" && exec -l "${SHELL}";
}
alias fgh=find-ghq

# gcloud
function safe-gcloud() {
    local active_config=$(cat $HOME/.config/gcloud/active_config)
    if [[ $active_config == *"prd"* ]]; then
        echo -n "You're in a production environment. Are you sure you want to proceed (Y/yes): "
        read REPLY
        if [[ $REPLY =~ ^([Yy]|[Yy]es)$ ]]; then
            command gcloud "$@"
        else
            echo "Aborted!"
        fi
    else
        command gcloud "$@"
    fi
}
complete -o nospace -o default -F _python_argcomplete "safe-gcloud"
alias gcloud='safe-gcloud'

function gx() {
  input_name="$1"
  if [ -z "$input_name" ]; then
    line=$(command gcloud config configurations list | fzf --header-lines=1)
  else
    line=$(command gcloud config configurations list | grep "$input_name")
  fi
  name=$(echo "${line}" | awk '{print $1}')
  project=$(echo "${line}" | awk '{print $4}')
  echo "gcloud config configurations activate \"${name}\""
  command gcloud auth application-default set-quota-project "${name}"
  command gcloud config configurations activate "${name}"
}

function gx-complete() {
  _values $(command gcloud config configurations list | awk '{print $1}')
}
compdef gx-complete gx

function gcloud-config-create-set-project-account() {
  # Ë®≠ÂÆöÂêç„ÇíÂÖ•Âäõ
  echo -n "Enter configuration name: "
  read config_name

  # „Éó„É≠„Ç∏„Çß„ÇØ„ÉàID„ÇíÂÖ•Âäõ
  echo -n "Enter your project ID: "
  read project_id

  # „Ç¢„Ç´„Ç¶„É≥„Éà„ÇíÂÖ•Âäõ
  echo -n "Enter your account: "
  read account

  # gcloud configuration„Çí‰ΩúÊàê
  command gcloud config configurations create "${config_name}"
  
  # account„Å®project ID„ÇíË®≠ÂÆö
  command gcloud config set account "${account}"
  command gcloud config set project "${project_id}"
}

function kubectl-less () {
    if [[ -t 1 ]]
    then
        command kubectl "$@" | less -F -X -n
    else
        command kubectl "$@"
    fi
}

function safe-kubectl() {
    local current_context=$(command kubectl config current-context)
    if [[ $current_context == *"prd"* ]]; then
        echo -n "You're in a production context. If you want to continue, type y(es) and press Enter: "
        read REPLY
        if [[ $REPLY =~ ^([Yy]|[Yy]es)$ ]]; then
            kubectl-less "$@"
        else
            echo "Aborted!"
        fi
    else
        kubectl-less "$@"
    fi
}
compdef _kubectl safe-kubectl
# alias kubectl='safe-kubectl'  # 2024-10-21: Fig(Amazon Q)„Çí‰Ωø„ÅÑ„Åü„ÅÑ„Åü„ÇÅ‰Ωø„Çè„Å™„ÅÑ

create_branch_with_guide() {
  selected_option=$(cat <<EOF | fzf
new: Êú¨Áï™Êèê‰æõÂâç„ÅÆ0 ‚Üí 1„ÅÆÊñ∞Ë¶èÈñãÁô∫
feature: Êó¢Â≠ò„Ç∑„Çπ„ÉÜ„É†„Å∏„ÅÆÊ©üËÉΩËøΩÂä†„Éª‰ªïÊßòÂ§âÊõ¥„Å´‰º¥„ÅÜÂÆüË£Ö„ÅÆÂ§âÊõ¥
update: Êó¢Â≠ò„Ç∑„Çπ„ÉÜ„É†„Å´„Å§„ÅÑ„Å¶„ÄÅ‰ªïÊßòÂ§âÊõ¥„Çí‰º¥„Çè„Å™„ÅÑÂÆüË£Ö„ÅÆÊîπÂñÑ
replace: ÂÆüË£Ö„ÅÆÂ§âÊõ¥„Çí‰º¥„Çè„Å™„ÅÑ„ÄÅÊó¢Â≠ò„Éë„É©„É°„Éº„Çø„ÇÑÁí∞Â¢ÉÂ§âÊï∞Á≠â„ÅÆÂÄ§„ÅÆÂ§âÊõ¥
clean: Êó¢Â≠ò„Ç∑„Çπ„ÉÜ„É†„Å´„Å§„ÅÑ„Å¶„ÄÅÂÆüË£Ö„ÅÆÂ§âÊõ¥„Çí‰º¥„Çè„Å™„ÅÑ„Ç≥„Éº„Éâ„ÅÆÊîπÂñÑ
chore: Êó¢Â≠ò„Ç∑„Çπ„ÉÜ„É†„Å∏„ÅÆÂΩ±Èüø„Åå„Å™„ÅÑÁã¨Á´ã„Åó„ÅüÂØæÂøú
verify: ÊäÄË°ìÊ§úË®º
hotfix: Â§âÊõ¥ÈöúÂÆ≥ÔºàÊÑèÂõ≥„Åó„Å¶„ÅÑ„Å™„Åã„Å£„ÅüÊåôÂãï„ÅÆÁô∫ÁîüÔºâ„Å´ÂØæ„Åô„Çã‰øÆÊ≠£
fix: ‰∫õÁ¥∞„Å™Â§âÊõ¥ÈöúÂÆ≥„Å´ÂØæ„Åô„Çã‰øÆÊ≠£
EOF
  )

  selected_branch=$(echo "$selected_option" | cut -d':' -f1)
  echo -n "Enter the branch name: "
  read name
  branch_name="${selected_branch}/${name}"
  git branch "${branch_name}"
  git switch "${branch_name}"
}

alias gn='create_branch_with_guide'

# ref. https://sushichan044.hateblo.jp/entry/2025/06/06/003325
function fzf-worktree() {
    # Format of `git worktree list`: path commit [branch]
    local selected_worktree=$(git worktree list | fzf \
        --prompt="worktrees > " \
        --header="Select a worktree to cd into" \
        --preview="echo 'üì¶ Branch:' && git -C {1} branch --show-current && echo '' && echo 'üìù Changed files:' && git -C {1} status --porcelain | head -10 && echo '' && echo 'üìö Recent commits:' && git -C {1} log --oneline --decorate -10" \
        --preview-window="right:40%" \
        --reverse \
        --border \
        --ansi)

    if [ $? -ne 0 ]; then
        return 0
    fi

    if [ -n "$selected_worktree" ]; then
        local selected_path=${${(s: :)selected_worktree}[1]}

        if [ -d "$selected_path" ]; then
            if zle; then
                # Called from ZLE (keyboard shortcut)
                BUFFER="cd ${selected_path}"
                zle accept-line
            else
                # Called directly from command line
                cd "$selected_path"
            fi
        else
            echo "Directory not found: $selected_path"
            return 1
        fi
    fi

    # Only clear screen if ZLE is active
    if zle; then
        zle clear-screen
    fi
}

zle -N fzf-worktree
bindkey '^n' fzf-worktree
